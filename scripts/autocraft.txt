unsafe()

@&autocraftprefix = "&8[&bAutocraft&8]"
@&autocraftchatcolor = "&f"
@&autocrafthighlightcolor = "&7"
@&autocrafterrorcolor = "&c"



//
// Ende der Liste
//

unset(&args[])
unset(&item)
unset(&action)
unset(#count)
unset(#delay)

push(&args[],$$[1])
push(&args[],$$[2])
push(&args[],$$[3])
push(&args[],$$[4])
push(&args[],$$[5])
push(&args[],$$[6])

set(&item,%&args[0]%)
set(#count,%&args[1]%)

indexof(&args[],#isin,"delay")
if(#isin > -1)
  inc(#isin,1)
  set(#delay,%&args[%#isin%]%)
endif

unset(nochat)
indexof(&args[],#isin,"nochat")
if(#isin > -1)
  set(nochat)
endif

indexof(&args[],#isin,"developer")
if(#isin > -1)
  set(&action,"developer")
endif

endunsafe()
arraysize(@&queue[],#size)


if(&action=="")

  if(#size>0)
    
    push(@&queue[],"%&item%,%#count%,%#delay%,%nochat%")
  
  else
    
    push(@&queue[],"%&item%,%#count%,%#delay%,%nochat%")
    
    do()
    
      unsafe()
        
        unset(broken)
        split(",",%@&queue[0]%,&queueitem[])
        set(#count,%&queueitem[1]%)
        set(#delay,%&queueitem[2]%)
        set(nochat,%&queueitem[3]%)
        split(":",%&queueitem[0]%,&item[])
        arraysize(&item[],#hasmetadata)
        
        arraysize(@&autocraftrecipes[],#size)
        dec(#size,1)
        
        for(#i,0,%#size%)
        
          split(",",%@&autocraftrecipes[%#i%]%,&recipetotest[])
          
          if(#hasmetadata>1)
            if(%&recipetotest[0]%==%&queueitem[0]%)
            
              set(broken)
              for(#i2,0,9)
              
                &recipe[%#i2%]= "%&recipetotest[%#i2%]%"
                
              next
              
              break
            
            endif
          else
            split(":",%&recipetotest[0]%,&recipeitem[])
            if(&recipeitem[0]==&queueitem[0])
            
              set(broken)
              for(#i2,0,9)
              
                split(":",%&recipetotest[%#i2%]%,&recp[])
                &recipe[%#i2%]= "%&recp[0]%"
                
              next
              
              break
            
            endif
          endif
        next
        
      endunsafe()
      
      if(broken)
      
        if(GUI=="GUICRAFTING")
        
        else
      
          log("%@&autocraftprefix%%@&autocraftchatcolor% Bitte gehe nun an eine Werkbank")
          
          do()
          
          while(!(GUI=="GUICRAFTING"))
          
        endif
      
//-----------------------------------------------------------------------------------------------------------------------------------------------------------
// Autocraft anfang
//-----------------------------------------------------------------------------------------------------------------------------------------------------------
//
//        Übergebene Variabeln:
// ----------------------------------
// &recipe[0]               = produkt
// &recipe[1] -> &recipe[9] = rezept
// #hasmetadata            := 1 = keine Metadata; 2 = mit Metadata
// #count                   = anzahl an Items
//
//------||
        
        unsafe()
        
          unset(&mats[])
          unset(#mats[])
          unset(#matsinv[])
          for(#i,1,9)
            if(!(&recipe[%#i%]==""))
              indexof(&mats[],#isin,%&recipe[%#i%]%)
              if(#isin==-1)
                push(&mats[],%&recipe[%#i%]%)
                arraysize(&mats,#store)
                dec(#store,1)
                inc(#mats[%#store%],1)
              else
                inc(#mats[%#isin%],1)
              endif
            endif
          next
          
          for(#i,10,45)
          
            getslotitem(%#i%,&item,#stacksize,#data)
          
            if(#hasmetadata==1)
            
              indexof(&mats[],#isin,%&item%)
            
              if(#isin>-1)
              
                inc(#matsinv[%#isin%],%#stacksize%)
                
              endif
              
            else
            
              set(&item,"%&item%:%#data%")
            
              indexof(&mats[],#isin,%&item%)
            
              if(#isin>-1)
              
                inc(#matsinv[%#isin%],%#stacksize%)
                
              endif
            
            endif
            
          next
          
        //
        // Berechnen der Mengen
        // Übergebene Variabeln:
        // ---------------------
        // &mats[]    = benötigte Materialien
        // #mats[]    = benötigte anzahl an Materialien pro Craft
        // #matsinv[] = vorhandene anzahl an Materialien
        // #count     = anzahl gewünschter Items
        //||
          
          unset(missingitems)
          
          if(#count>=0)
          
            arraysize(&mats,#matssize)
            dec(#matssize,1)
            if(!nochat)
              log("%@&autocraftprefix%%@&autocraftchatcolor% Um %@&autocrafthighlightcolor%%#count% %&queueitem[0]%%@&autocraftchatcolor% zu Craften braucht man:")
            endif
            
            for(#i,0,%#matssize%)
            
              #matsneeded[%#i%] = %#mats[%#i%]%*%#count%  
              if(#matsneeded[%#i%]>#matsinv[%#i%])
                set(missingitems)
                if(!nochat)
                  log("%@&autocraftprefix%%@&autocrafterrorcolor% - %@&autocrafthighlightcolor%%#matsneeded[%#i%]% %&mats[%#i%]%%@&autocrafterrorcolor% , du hast: %#matsinv[%#i%]%/%#matsneeded[%#i%]%")
                endif
              else
                if(!nochat)
                  log("%@&autocraftprefix%%@&autocraftchatcolor% - %@&autocrafthighlightcolor%%#matsneeded[%#i%]% %&mats[%#i%]%%@&autocraftchatcolor% , du hast: %#matsinv[%#i%]%/%#matsneeded[%#i%]%")
                endif
              endif
            next
            
          else
          
            
            arraysize(&mats,#matssize)
            dec(#matssize,1)
            
            set(#amount,0)
            
            for(#i,0,%#matssize%)
            
              unset(#thisamount)
              #thisamount = %#matsinv[%#i%]%/%#mats[%#i%]%

              if(#amount=0 || #thisamount < #amount)
              
                #amount = #thisamount
                
              endif
            
            next
            
            #count = #amount
            
            if(!nochat)
              log("%@&autocraftprefix%%@&autocraftchatcolor% Um %@&autocrafthighlightcolor%%#count% %&queueitem[0]%%@&autocraftchatcolor% zu Craften braucht man:")
            endif
            
            for(#i,0,%#matssize%)
            
              #matsneeded[%#i%] = %#mats[%#i%]%*%#count%  
              if(#matsneeded[%#i%]>#matsinv[%#i%])
                set(missingitems)
                if(!nochat)
                  log("%@&autocraftprefix%%@&autocrafterrorcolor% - %@&autocrafthighlightcolor%%#matsneeded[%#i%]% %&mats[%#i%]%%@&autocrafterrorcolor% , du hast: %#matsinv[%#i%]%/%#matsneeded[%#i%]%")
                endif
              else
                if(!nochat)
                  log("%@&autocraftprefix%%@&autocraftchatcolor% - %@&autocrafthighlightcolor%%#matsneeded[%#i%]% %&mats[%#i%]%%@&autocraftchatcolor% , du hast: %#matsinv[%#i%]%/%#matsneeded[%#i%]%")
                endif
              endif
            next
          
          endif
          
        //||
        // Ende des Berechnens
        //
        
        endunsafe()  
        
        unset(broken)
        
        if(!missingitems)
        
          if(#count == 0)
          
            if(!nochat)
              log("%@&autocraftprefix%%@&autocraftchatcolor% %@&autocrafterrorcolor%Dir fehlen Items.")
            endif
            
          else
        
            #stacks = 1
          
            do()
              if(#count>64)
                inc(#stacks,1)
                dec(#count,64)
              endif
            while(#count>64)
             
              
            do(%#stacks%)
              
              
              if(#count==0)
                #craft=64
              else
                #craft=%#count%
              endif
              
              for(#i,1,9)          
                
                if(!(&recipe[%#i%]==""))
                  
                  do()
              
                    unsafe(20)
              
                      getslotitem(%#i%,,#craftstacksize)
                      getslot(%&recipe[%#i%]%,#slot,10)
                      getslotitem(%#slot%,,#stacksize)
                      #missing=%#craft%-%#craftstacksize%
                      slotclick(%#slot%)
                      
                      if(#stacksize>#missing)
                        #lmb = %#stacksize%-%#missing%
                        do(%#lmb%)
                          slotclick(%#slot%,RMB)
                        loop
                        slotclick(%#i%)
                      elseif(#stacksize==#craft)
                        slotclick(%#i%)
                      else
                        slotclick(%#i%)
                      endif
                      
                      getslotitem(%#i%,,#craftstacksize)
                    
                    endunsafe()
                    
                  until(#craftstacksize==#craft)
                  
                endif
            
              next
              
              slotclick(0,,true)
              if(#delay >0)
                wait(%#delay%ms)
              endif
              
              #count=0
              
              
            loop
          
            if(!nochat)
              log("%@&autocraftprefix%%@&autocraftchatcolor% %@&autocrafthighlightcolor%\"%&queueitem[0]%\"%@&autocraftchatcolor% erfolgreich hergestellt")
            endif
            
          endif
        
        else

        endif 
          
        
//------||
//-----------------------------------------------------------------------------------------------------------------------------------------------------------
// Autocraft ende
//-----------------------------------------------------------------------------------------------------------------------------------------------------------
      
        unsafe()
      
          arraysize(@&queue[],#size)
          if(#size>1)
            dec(#size,1)
            for(#i,1,%#size%)
              set(#idwn,%#i%-1)
              set(@&queue[%#idwn%],%@&queue[%#i%]%)
            next
            unset(@&queue[%#size%])
            
          else
          
            unset(@&queue[])
            
          endif
        
        endunsafe()
        
      
      else
      
        arraysize(@&queue[],#size)
        if(#size>1)
          dec(#size,1)
          for(#i,1,%#size%)
            set(#idwn,%#i%-1)
            set(@&queue[%#idwn%],%@&queue[%#i%]%)
          next
          unset(@&queue[%#size%])
          
        else
        
          unset(@&queue[])
          
        endif
        
        log("%@&autocraftprefix%%@&autocrafterrorcolor% Für %@&autocrafthighlightcolor%\"%&queueitem[0]%\"%@&autocrafterrorcolor% ist mir kein Rezept bekannt :c")
      endif
    
      arraysize(@&queue[],#size)
    
    while(#size>0)
    
  endif
  
elseif(&action=="developer")

  prompt(&devaction,"$$[[Rmv,Queue]]")
  
  if(&devaction=="Rmv")
    log("rmv")
    unset(@&autocraftrecipes[])
  elseif(&devaction=="Queue")
    log("queue")
    unset(@&queue[])
  endif
  
endif