$${

//Jsoner (Minecraft color codes to JSON);
//Made by TKM#6228 :) ;
//Feel free to ask me anything!;

//Using prompt to input the text;
prompt(&input,"$$?","Text to convert to json:");
if(&input == "");
	log("No Input");
	log("[\"\"]");
	stop();
endif;
split("","%&input%…",&array[]);
//The '…' is used to show the end of the string;

//Always declare stuff;
set(&json,"[\"\"");
set(&text,"");
set(&specialtext,"");
set(&color,"");
set(&special,"");
unset(bold);
unset(italic);
unset(underline);
unset(strike);
unset(obfuscate);
unset(compile);
unset(code);
unset(s);

//Parse through each letter;
//Unsafe is used to make the program run faster, take note it might cause some lag;
unsafe(10000);
foreach(&array[],&item);
	set(&temp,"");
	set(&tempc,"");
	set(&temps,"");
	//Some temporary variables to take note of the color code's effect (style/color/special);
	if(code);
		//Translate color/special code letter;
		if(&item == "U");
			set(&temps,"url");
		elseif(&item == "C");
			set(&temps,"runcommand");
		elseif(&item == "S");
			set(&temps,"suggestcommand");
		elseif(&item == "T");
			set(&temps,"showtext");
		elseif(&item == "I");
			set(&temps,"showitem");
		elseif(&item == "E");
			set(&temps,"showentity");
		elseif(&item == "V");
			set(&temps,"insert");
		elseif(&item == "O");
			set(&temps,"score");
		elseif(&item == "P");
			set(&temps,"select");
		elseif(&item == "K");
			set(&temps,"key");
		elseif(&special == "");
			//Normal Minecraft color codes/format;
			if(&item == "l");
				set(&temp,"bold");
			elseif(&item == "o");
				set(&temp,"italic");
			elseif(&item == "n");
				set(&temp,"underline");
			elseif(&item == "m");
				set(&temp,"strike");
			elseif(&item == "k");
				set(&temp,"obfuscate");
			elseif(&item == "r");
				log("reseted");
			elseif(&item == "0");
				set(&tempc,"black");
			elseif(&item == "1");
				set(&tempc,"dark_blue");
			elseif(&item == "2");
				set(&tempc,"dark_green");
			elseif(&item == "3");
				set(&tempc,"dark_aqua");
			elseif(&item == "4");
				set(&tempc,"dark_red");
			elseif(&item == "5");
				set(&tempc,"dark_purple");
			elseif(&item == "6");
				set(&tempc,"gold");
			elseif(&item == "7");
				set(&tempc,"gray");
			elseif(&item == "8");
				set(&tempc,"dark_gray");
			elseif(&item == "9");
				set(&tempc,"blue");
			elseif(&item == "a");
				set(&tempc,"green");
			elseif(&item == "b");
				set(&tempc,"aqua");
			elseif(&item == "c");
				set(&tempc,"red");
			elseif(&item == "d");
				set(&tempc,"light_purple");
			elseif(&item == "e");
				set(&tempc,"yellow");
			elseif(&item == "f");
				set(&tempc,"white");
			elseif(&item == "&");
				set(&text,"%&text%%&item%");
			else;
				set(&text,"%&text%&%&item%");
			endif;
		endif;
		//Toggle special code, to see if it is the start or the end of the special codes;
		if(&temps != "");
			if(&special == &temps);
				set(compile);
			elseif(&special == "");
				set(&special,%&temps%);
			endif;
		endif;
		set(&temps,"");
		//See if the color codes are new, that there is no color codes before;
		//Used to 'reset' the style when there is a new color;
		if((&text != "") && (&special == ""));
			set(compile);
		endif;
		unset(code);
	else;
		//Check if the item is a prefix or other stuff;
		if(&item == "&");
			set(code);
		elseif(&item == "…");
			set(compile);
		elseif((&special != "") && (&special != "showtext"));
			set(&specialtext,%&specialtext%%&item%);
		//showtext is special, because you can show colors when using hoverevent;
		elseif(&special != "showtext");
			set(&text,"%&text%%&item%");
		endif;
	endif;
	if(&special == "showtext");
		set(&specialtext,"%&specialtext%%&item%");
	endif;
	//To combine the formatting and text together, append it to &json;
	if(compile);
		if(&text != "");
			set(&json,"%&json%,{\"text\":\"%&text%\"");
			if(bold);
				set(&json,"%&json%,\"bold\":true");
			endif;
			if(italic);
				set(&json,"%&json%,\"italic\":true");
			endif;
			if(underline);
				set(&json,"%&json%,\"underlined\":true");
			endif;
			if(strike);
				set(&json,"%&json%,\"strikethrough\":true");
			endif;
			if(obfuscate);
				set(&json,"%&json%,\"obfuscated\":true");
			endif;
			if(&color != "");
				set(&json,"%&json%,\"color\":\"%&color%\"");
			endif;
			if(&special == "");
				set(&json,"%&json%}");
			else;
				set(s);
				//s is a boolean to see if special was used in if/elseif;
				if(&special == "url");
					set(&json,"%&json%,\"clickEvent\":{\"action\":\"open_url\",\"value\":\"%&specialtext%\"}");
				elseif(&special == "runcommand");
					set(&json,"%&json%,\"clickEvent\":{\"action\":\"run_command\",\"value\":\"%&specialtext%\"}");
				elseif(&special == "suggestcommand");
					set(&json,"%&json%,\"clickEvent\":{\"action\":\"suggest_command\",\"value\":\"%&specialtext%\"}");
				elseif(&special == "showtext");

					//This part is to make the hover show_text tooltip colored, mostly just a repeat of the above code;
					//I made all variables end with 2 to prevent it from rewritting previous variables;
					split("","%&specialtext%",&array2[]);
					set(first);
					set(&json2,"[\"\"");
					set(&text2,"");
					set(&color2,"");
					unset(bold2);
					unset(italic2);
					unset(underline2);
					unset(strike2);
					unset(obfuscate2);
					unset(compile2);
					unset(code2);
					foreach(&array2[],&item2);
						if(first);
							unset(first);
						else;
							set(&temp2,"");
							set(&tempc2,"");
							if(code2);
								if(&item2 == "l");
									set(&temp2,"bold2");
								elseif(&item2 == "o");
									set(&temp2,"italic2");
								elseif(&item2 == "n");
									set(&temp2,"underline2");
								elseif(&item2 == "m");
									set(&temp2,"strike2");
								elseif(&item2 == "k");
									set(&temp2,"obfuscate2");
								elseif(&item2 == "r");
									set(&temp2,"reset2");
								elseif(&item2 == "0");
									set(&tempc2,"black");
								elseif(&item2 == "1");
									set(&tempc2,"dark_blue");
								elseif(&item2 == "2");
									set(&tempc2,"dark_green");
								elseif(&item2 == "3");
									set(&tempc2,"dark_aqua");
								elseif(&item2 == "4");
									set(&tempc2,"dark_red");
								elseif(&item2 == "5");
									set(&tempc2,"dark_purple");
								elseif(&item2 == "6");
									set(&tempc2,"gold");
								elseif(&item2 == "7");
									set(&tempc2,"gray");
								elseif(&item2 == "8");
									set(&tempc2,"dark_gray");
								elseif(&item2 == "9");
									set(&tempc2,"blue");
								elseif(&item2 == "a");
									set(&tempc2,"green");
								elseif(&item2 == "b");
									set(&tempc2,"aqua");
								elseif(&item2 == "c");
									set(&tempc2,"red");
								elseif(&item2 == "d");
									set(&tempc2,"light_purple");
								elseif(&item2 == "e");
									set(&tempc2,"yellow");
								elseif(&item2 == "f");
									set(&tempc2,"white");
								elseif(&item2 == "&");
									set(&text2,"%&text2%%&item2%");
								elseif(&item2 == "T");
									set(compile2);
								else;
									set(&text2,"%&text2%&%&item2%");
								endif;
								if(&text2 != "");
									set(compile2);
								endif;
								unset(code2);
							else;
								if(&item2 == "&");
									set(code2);
								else;
									set(&text2,"%&text2%%&item2%");
								endif;
							endif;
							if(compile2);
								set(&json2,"%&json2%,{\"text\":\"%&text2%\"");
								if(bold2);
									set(&json2,"%&json2%,\"bold\":true");
								endif;
								if(italic2);
									set(&json2,"%&json2%,\"italic\":true");
								endif;
								if(underline2);
									set(&json2,"%&json2%,\"underlined\":true");
								endif;
								if(strike2);
									set(&json2,"%&json2%,\"strikethrough\":true");
								endif;
								if(obfuscate2);
									set(&json2,"%&json2%,\"obfuscated\":true");
								endif;
								if(&color2 != "");
									set(&json2,"%&json2%,\"color\":\"%&color2%\"");
								endif;
								set(&json2,"%&json2%}");
								set(&text2,"");
								unset(compile2);
							endif;
							if(&temp2 == "reset2");
								unset(bold2);
								unset(italic2);
								unset(underline2);
								unset(strike2);
								unset(obfuscate2);
								set(&color2,"");
							elseif(&tempc2 != "");
								set(&color2,%&tempc2%);
								unset(bold2);
								unset(italic2);
								unset(underline2);
								unset(strike2);
								unset(obfuscate2);
								set(&tempc2,"");
							elseif(&temp2 != "");
								set(%&temp2%);
							endif;
						endif;
					next;
					set(&json2,"%&json2%]");
					//End of json 2;

					set(&json,"%&json%,\"hoverEvent\":{\"action\":\"show_text\",\"value\":%&json2%}");
				elseif(&special == "showitem");
					set(&json,"%&json%,\"hoverEvent\":{\"action\":\"show_item\",\"value\":\"{%&specialtext%}\"}");
				elseif(&special == "showentity");
					set(&json,"%&json%,\"hoverEvent\":{\"action\":\"show_entity\",\"value\":\"{%&specialtext%}\"}");
				elseif(&special == "insert");
					set(&json,"%&json%,\"insertion\":\"%&specialtext%\"");
				else;
					unset(s);
				endif;
				if(s);
					set(&special,"");
					set(&specialtext,"");
				endif;
				set(&json,"%&json%}");
			endif;
		endif;
		//These are to insert the selectors, score, and key, and it replaces the "text";
		if(&special != "");
			if(&special == "select");
				set(&json,"%&json%,{\"selector\":\"%&specialtext%\"");
			elseif(&special == "score");
				split(",",%&specialtext%,&scores[]);
				set(&json,"%&json%,{\"score\":{\"name\":\"%&scores[0]%\",\"objective\":\"%&scores[1]%\"}");
			elseif(&special == "key");
				set(&json,"%&json%,{\"keybind\":\"%&specialtext%\"");
			endif;
			if(bold);
				set(&json,"%&json%,\"bold\":true");
			endif;
			if(italic);
				set(&json,"%&json%,\"italic\":true");
			endif;
			if(underline);
				set(&json,"%&json%,\"underlined\":true");
			endif;
			if(strike);
				set(&json,"%&json%,\"strikethrough\":true");
			endif;
			if(obfuscate);
				set(&json,"%&json%,\"obfuscated\":true");
			endif;
			if(&color != "");
				set(&json,"%&json%,\"color\":\"%&color%\"");
			endif;
			set(&json,"%&json%}");
			set(&special,"");
			set(&specialtext,"");
		endif;
		set(&text,"");
		unset(compile);
	endif;
	//Making temp variables into the format;
	if(&temp == "reset");
		unset(bold);
		unset(italic);
		unset(underline);
		unset(strike);
		unset(obfuscate);
		set(&color,"");
	elseif(&tempc != "");
		set(&color,%&tempc%);
		unset(bold);
		unset(italic);
		unset(underline);
		unset(strike);
		unset(obfuscate);
		set(&tempc,"");
	elseif(&temp != "");
		set(%&temp%);
	endif;
next;
endunsafe;

set(&json,"%&json%]");

//You can do whatever here, some examples below:;
//log("&6&ljson: &e%&json%");
//lograw(%&json%);
//setclipboard(%&json%);
//echo("/tellraw @p %&json%");
//echo("/title @p actionbar %&json%");

}$$