$${

//Jsoner (Minecraft color codes to JSON);
//Made by TKM#6228 :) ;
//Feel free to ask me anything!;

//Using prompt to input the text;
prompt(&input,"$$?","Text to convert to json:");
if(&input == "");
	log("No Input");
	log("[\"\"]");
	stop();
endif;
split("","%&input%…",&array[]);
//The '…' is used to show the end of the string;

//Always declare stuff;
&json = "[\"\"";
&text = "";
&specialtext = "";
&color = "";
&special = "";
unset(bold);
unset(italic);
unset(underline);
unset(strike);
unset(obfuscate);
unset(compile);
unset(code);
unset(s);

//Parse through each letter;
//Unsafe is used to make the program run faster, take note it might cause some lag;
unsafe(10000);
foreach(&array[],&item);
	&temp = "";
	&tempc = "";
	&temps = "";
	//Some temporary variables to take note of the color code's effect (style/color/special);
	if(code);
		//Translate color/special code letter;
		if(&item == "U");
			&temps = "url";
		elseif(&item == "C");
			&temps = "runcommand";
		elseif(&item == "S");
			&temps = "suggestcommand";
		elseif(&item == "T");
			&temps = "showtext";
		elseif(&item == "I");
			&temps = "showitem";
		elseif(&item == "E");
			&temps = "showentity";
		elseif(&item == "V");
			&temps = "insert";
		elseif(&item == "O");
			&temps = "score";
		elseif(&item == "P");
			&temps = "select";
		elseif(&item == "K");
			&temps = "key";
		elseif(&special == "");
			//Normal Minecraft color codes/format;
			if(&item == "l");
				&temp = "bold";
			elseif(&item == "o");
				&temp = "italic";
			elseif(&item == "n");
				&temp = "underline";
			elseif(&item == "m");
				&temp = "strike";
			elseif(&item == "k");
				&temp = "obfuscate";
			elseif(&item == "r");
				log("reseted");
			elseif(&item == "0");
				&tempc = "black";
			elseif(&item == "1");
				&tempc = "dark_blue";
			elseif(&item == "2");
				&tempc = "dark_green";
			elseif(&item == "3");
				&tempc = "dark_aqua";
			elseif(&item == "4");
				&tempc = "dark_red";
			elseif(&item == "5");
				&tempc = "dark_purple";
			elseif(&item == "6");
				&tempc = "gold";
			elseif(&item == "7");
				&tempc = "gray";
			elseif(&item == "8");
				&tempc = "dark_gray";
			elseif(&item == "9");
				&tempc = "blue";
			elseif(&item == "a");
				&tempc = "green";
			elseif(&item == "b");
				&tempc = "aqua";
			elseif(&item == "c");
				&tempc = "red";
			elseif(&item == "d");
				&tempc = "light_purple";
			elseif(&item == "e");
				&tempc = "yellow";
			elseif(&item == "f");
				&tempc = "white";
			elseif(&item == "&");
				&text = "%&text%%&item%";
			else;
				&text = "%&text%&%&item%";
			endif;
		endif;
		//Toggle special code, to see if it is the start or the end of the special codes;
		if(&temps != "");
			if(&special == &temps);
				set(compile);
			elseif(&special == "");
				&special = %&temps%;
			endif;
		endif;
		&temps = "";
		//See if the color codes are new, that there is no color codes before;
		//Used to 'reset' the style when there is a new color;
		if((&text != "") && (&special == ""));
			set(compile);
		endif;
		unset(code);
	else;
		//Check if the item is a prefix or other stuff;
		if(&item == "&");
			set(code);
		elseif(&item == "…");
			set(compile);
		elseif((&special != "") && (&special != "showtext"));
			&specialtext = %&specialtext%%&item%;
		//showtext is special, because you can show colors when using hoverevent;
		elseif(&special != "showtext");
			&text = "%&text%%&item%";
		endif;
	endif;
	if(&special == "showtext");
		&specialtext = "%&specialtext%%&item%";
	endif;
	//To combine the formatting and text together, append it to &json;
	if(compile);
		if(&text != "");
			&json = "%&json%,{\"text\":\"%&text%\"";
			if(bold);
				&json = "%&json%,\"bold\":true";
			endif;
			if(italic);
				&json = "%&json%,\"italic\":true";
			endif;
			if(underline);
				&json = "%&json%,\"underlined\":true";
			endif;
			if(strike);
				&json = "%&json%,\"strikethrough\":true";
			endif;
			if(obfuscate);
				&json = "%&json%,\"obfuscated\":true";
			endif;
			if(&color != "");
				&json = "%&json%,\"color\":\"%&color%\"";
			endif;
			if(&special == "");
				&json = "%&json%}";
			else;
				set(s);
				//s is a boolean to see if special was used in if/elseif;
				if(&special == "url");
					&json = "%&json%,\"clickEvent\":{\"action\":\"open_url\",\"value\":\"%&specialtext%\"}";
				elseif(&special == "runcommand");
					&json = "%&json%,\"clickEvent\":{\"action\":\"run_command\",\"value\":\"%&specialtext%\"}";
				elseif(&special == "suggestcommand");
					&json = "%&json%,\"clickEvent\":{\"action\":\"suggest_command\",\"value\":\"%&specialtext%\"}";
				elseif(&special == "showtext");

					//This part is to make the hover show_text tooltip colored, mostly just a repeat of the above code;
					//I made all variables end with 2 to prevent it from rewritting previous variables;
					split("","%&specialtext%",&array2[]);
					set(first);
					&json2 = "[\"\"";
					&text2 = "";
					&color2 = "";
					unset(bold2);
					unset(italic2);
					unset(underline2);
					unset(strike2);
					unset(obfuscate2);
					unset(compile2);
					unset(code2);
					foreach(&array2[],&item2);
						if(first);
							unset(first);
						else;
							&temp2 = "";
							&tempc2 = "";
							if(code2);
								if(&item2 == "l");
									&temp2 = "bold2";
								elseif(&item2 == "o");
									&temp2 = "italic2";
								elseif(&item2 == "n");
									&temp2 = "underline2";
								elseif(&item2 == "m");
									&temp2 = "strike2";
								elseif(&item2 == "k");
									&temp2 = "obfuscate2";
								elseif(&item2 == "r");
									&temp2 = "reset2";
								elseif(&item2 == "0");
									&tempc2 = "black";
								elseif(&item2 == "1");
									&tempc2 = "dark_blue";
								elseif(&item2 == "2");
									&tempc2 = "dark_green";
								elseif(&item2 == "3");
									&tempc2 = "dark_aqua";
								elseif(&item2 == "4");
									&tempc2 = "dark_red";
								elseif(&item2 == "5");
									&tempc2 = "dark_purple";
								elseif(&item2 == "6");
									&tempc2 = "gold";
								elseif(&item2 == "7");
									&tempc2 = "gray";
								elseif(&item2 == "8");
									&tempc2 = "dark_gray";
								elseif(&item2 == "9");
									&tempc2 = "blue";
								elseif(&item2 == "a");
									&tempc2 = "green";
								elseif(&item2 == "b");
									&tempc2 = "aqua";
								elseif(&item2 == "c");
									&tempc2 = "red";
								elseif(&item2 == "d");
									&tempc2 = "light_purple";
								elseif(&item2 == "e");
									&tempc2 = "yellow";
								elseif(&item2 == "f");
									&tempc2 = "white";
								elseif(&item2 == "&");
									&text2 = "%&text2%%&item2%";
								elseif(&item2 == "T");
									set(compile2);
								else;
									&text2 = "%&text2%&%&item2%";
								endif;
								if(&text2 != "");
									set(compile2);
								endif;
								unset(code2);
							else;
								if(&item2 == "&");
									set(code2);
								else;
									&text2 = "%&text2%%&item2%";
								endif;
							endif;
							if(compile2);
								&json2 = "%&json2%,{\"text\":\"%&text2%\"";
								if(bold2);
									&json2 = "%&json2%,\"bold\":true";
								endif;
								if(italic2);
									&json2 = "%&json2%,\"italic\":true";
								endif;
								if(underline2);
									&json2 = "%&json2%,\"underlined\":true";
								endif;
								if(strike2);
									&json2 = "%&json2%,\"strikethrough\":true";
								endif;
								if(obfuscate2);
									&json2 = "%&json2%,\"obfuscated\":true";
								endif;
								if(&color2 != "");
									&json2 = "%&json2%,\"color\":\"%&color2%\"";
								endif;
								&json2 = "%&json2%}";
								&text2 = "";
								unset(compile2);
							endif;
							if(&temp2 == "reset2");
								unset(bold2);
								unset(italic2);
								unset(underline2);
								unset(strike2);
								unset(obfuscate2);
								&color2 = "";
							elseif(&tempc2 != "");
								&color2 = %&tempc2%;
								unset(bold2);
								unset(italic2);
								unset(underline2);
								unset(strike2);
								unset(obfuscate2);
								&tempc2 = "";
							elseif(&temp2 != "");
								set(%&temp2%);
							endif;
						endif;
					next;
					&json2 = "%&json2%]";
					//End of json 2;

					&json = "%&json%,\"hoverEvent\":{\"action\":\"show_text\",\"value\":%&json2%}";
				elseif(&special == "showitem");
					&json = "%&json%,\"hoverEvent\":{\"action\":\"show_item\",\"value\":\"{%&specialtext%}\"}";
				elseif(&special == "showentity");
					&json = "%&json%,\"hoverEvent\":{\"action\":\"show_entity\",\"value\":\"{%&specialtext%}\"}";
				elseif(&special == "insert");
					&json = "%&json%,\"insertion\":\"%&specialtext%\"";
				else;
					unset(s);
				endif;
				if(s);
					&special = "";
					&specialtext = "";
				endif;
				&json = "%&json%}";
			endif;
		endif;
		//These are to insert the selectors, score, and key, and it replaces the "text";
		if(&special != "");
			if(&special == "select");
				&json = "%&json%,{\"selector\":\"%&specialtext%\"";
			elseif(&special == "score");
				split(",",%&specialtext%,&scores[]);
				&json = "%&json%,{\"score\":{\"name\":\"%&scores[0]%\",\"objective\":\"%&scores[1]%\"}";
			elseif(&special == "key");
				&json = "%&json%,{\"keybind\":\"%&specialtext%\"";
			endif;
			if(bold);
				&json = "%&json%,\"bold\":true";
			endif;
			if(italic);
				&json = "%&json%,\"italic\":true";
			endif;
			if(underline);
				&json = "%&json%,\"underlined\":true";
			endif;
			if(strike);
				&json = "%&json%,\"strikethrough\":true";
			endif;
			if(obfuscate);
				&json = "%&json%,\"obfuscated\":true";
			endif;
			if(&color != "");
				&json = "%&json%,\"color\":\"%&color%\"";
			endif;
			&json = "%&json%}";
			&special = "";
			&specialtext = "";
		endif;
		&text = "";
		unset(compile);
	endif;
	//Making temp variables into the format;
	if(&temp == "reset");
		unset(bold);
		unset(italic);
		unset(underline);
		unset(strike);
		unset(obfuscate);
		&color = "";
	elseif(&tempc != "");
		&color = %&tempc%;
		unset(bold);
		unset(italic);
		unset(underline);
		unset(strike);
		unset(obfuscate);
		&tempc = "";
	elseif(&temp != "");
		set(%&temp%);
	endif;
next;
endunsafe;

&json = "%&json%]";

//You can do whatever here, some examples below:;
//log("&6&ljson: &e%&json%");
//lograw(%&json%);
//setclipboard(%&json%);
//echo("/tellraw @p %&json%");
//echo("/title @p actionbar %&json%");

}$$